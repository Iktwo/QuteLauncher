buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.0'
        classpath 'com.google.gms:google-services:3.0.0'
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    compileSdkVersion androidCompileSdkVersion.toInteger()

    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.iktwo.qutelauncher"
    }

    String APK_NAME = "qutelauncher"

    String BUILDTYPE = ""

    productFlavors {
        full {

        }

        noanalytics {

        }
    }

    buildTypes {
        releaseUnsigned {
            BUILDTYPE = "unsigned"
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }

        full {
            java.srcDirs = ['full']
            manifest.srcFile 'full/AndroidManifest.xml'
        }

        noanalytics {
            java.srcDirs = ['noanalytics']
        }
    }

    lintOptions {
        abortOnError false
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    APK_NAME + "_" + variant.productFlavors[0].name + "_"+ BUILDTYPE + ".apk")
        }

        variant.assemble.doLast {
            variant.outputs.each { output ->
                println "aligned " + output.outputFile
                println "unaligned " + output.packageApplication.outputFile

                File unaligned = output.packageApplication.outputFile;
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
    }
}

dependencies {
    fullCompile 'com.google.firebase:firebase-core:9.4.0'
    fullCompile 'com.android.support:appcompat-v7:24.2.0'
    fullCompile 'com.google.firebase:firebase-messaging:9.4.0'
    fullCompile 'com.google.firebase:firebase-crash:9.4.0'
}
