buildscript {
    ext {
        kotlin_version = '1.3.0'
        gradle_version = '3.3.1'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.2.61"
}

android {
    /*******************************************************
     * The following variables:
     * - androidBuildToolsVersion,
     * - androidCompileSdkVersion
     * - qt5AndroidDir - holds the path to qt android files
     *                   needed to build any Qt application
     *                   on Android.
     *
     * are defined in gradle.properties file. This file is
     * updated by QtCreator and androiddeployqt tools.
     * Changing them manually might break the compilation!
     *******************************************************/

    /**
     * Uncomment the following lines and remove minsdk from manifest to sync inside IntelliJ:
     *
     * String androidBuildToolsVersion = "28.0.3"
     * String androidCompileSdkVersion = "28"
     * String qt5AndroidDir = ""
     */


    compileSdkVersion androidCompileSdkVersion.toInteger()

    buildToolsVersion androidBuildToolsVersion

    defaultConfig {
        applicationId "com.iktwo.qutelauncher"
        targetSdkVersion 28
    }

    String APK_NAME = "qutelauncher"

    productFlavors {
        full {
            dimension "analytics"
        }

        noanalytics {
            dimension "analytics"
        }
    }

    flavorDimensions "analytics"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = [qt5AndroidDir + '/src', 'src', 'java']
            aidl.srcDirs = [qt5AndroidDir + '/src', 'src', 'aidl']
            res.srcDirs = [qt5AndroidDir + '/res', 'res']
            resources.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs = ['libs']
       }

        full {
            java.srcDirs = ['full']
            manifest.srcFile 'full/AndroidManifest.xml'
        }

        noanalytics {
            java.srcDirs = ['noanalytics']
        }
    }

    lintOptions {
        abortOnError false
    }

    afterEvaluate {
        assembleRelease.doLast {

        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            output.outputFileName = [APK_NAME, variant.productFlavors[0].name, variant.buildType.name].join("_") + ".apk"
        }

        variant.assemble.doLast {
            variant.outputs.all { output ->
                String apk_name = [APK_NAME, variant.productFlavors[0].name, variant.buildType.name].join("_") + ".apk"

                if (variant.productFlavors[0].name == "full") {
                    copy {
                        from output.outputFile
                        into file(buildDir.getPath() + "/outputs/apk/")
                        rename { String fileName ->
                            fileName.replace(apk_name, "android-build-" + variant.buildType.name + ".apk")
                        }
                    }
                }
            }
        }
    }
}
